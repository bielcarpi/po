<programa> ::= <declaracioFuncioMain> <llistaDeclaracions>
<llistaDeclaracions> ::= <declaracio> <llistaDeclaracions> | ε
<declaracio> ::= <declaracioTipus> | <declaracioFuncio>
<declaracioTipus> ::= type ID { \n <declaracioVariableTipus> } \n
<declaracioVariableTipus> ::= var ID \n
<assignacioVariable> ::= ID -> ID = <expressio> \n
<declaracioVariableID> ::= ID
<declaracioFuncio> ::= func ID ( <parametres> ) <sentenciaComposta> \n
<declaracioFuncioMain> ::= func main ( ) <sentenciaComposta> \n
<parametres> ::= <llistaParametres> | ε
<llistaParametres> ::= <declaracioVariableParametre> <llistaParametresPrima>
<llistaParametresPrima> ::= , <declaracioVariableParametre> <llistaParametresPrima> | ε
<declaracioVariableParametre> ::= <declaracioVariableID>
<sentencia> ::= <assignacioVariable> | <sentenciaCondicional> | <sentenciaWhile> | <sentenciaFor> | <sentenciaSwitch> | <sentenciaRetorn> | <sentenciaBreak>
<sentenciaComposta> ::= { \n <llistaComposta> }
<llistaComposta> ::= <declaracioBloc> <llistaComposta> | ε
<declaracioBloc> ::= <declaracioTipus> | <sentencia>
<sentenciaCondicional> ::= if <expressio>  <sentenciaComposta> <llistaElsif> <blocElse> \n
<llistaElsif> ::= <blocElsif> <llistaElsif> | ε
<blocElsif> ::= elsif  <expressio> <sentenciaComposta>
<blocElse> ::= else <sentenciaComposta> | ε
<sentenciaSwitch> ::= switch <expressio> <blocSwitch>
<blocSwitch> ::= { \n <llistaOpt> <blocDefault> } \n
<llistaOpt> ::= <blocOpt> <llistaOptPrima>
<llistaOptPrima> ::= <blocOpt> <llistaOptPrima> | ε
<blocOpt> ::= opt <expressio> -> <sentenciaComposta> \n
<blocDefault> ::= default -> <sentenciaComposta> \n | ε
<sentenciaRetorn> ::= ret <expressio> \n
<sentenciaBreak> ::= break \n
<sentenciaWhile> ::= while <expressio> <sentenciaComposta> \n
<sentenciaFor> ::= for ID -> ID <continuacioFor>
<continuacioFor> ::= = <expressio> <fori> | <foreach>
<fori> ::= ,  <expressio> , <expressio> <sentenciaComposta> \n
<foreach> ::= in <expressio> <sentenciaComposta> \n
<expressio> ::= <expressioAnd> <expressioPrima>
<expressioPrima> ::= or <expressioAnd> <expressioPrima> | ε
<expressioAnd> ::= <expressioNot> <expressioAndPrima>
<expressioAndPrima> ::= and <expressioNot> <expressioAndPrima> | ε
<expressioNot> ::= not <expressioNot> | <expressioRel>
<expressioRel> ::= <expressioSuma> <continuacioRel>
<continuacioRel> ::= <operador> <expressioSuma> | ε
<operador> ::= < | > | <= | >= | == | !=
<expressioSuma> ::= <expressioMul> <expressioSumaPrima>
<expressioSumaPrima> ::= <operadorSuma> <expressioMul> <expressioSumaPrima> | ε
<operadorSuma> ::= + | -
<expressioMul> ::= <expressioUnaria> <expressioMulPrima>
<expressioMulPrima> ::= <operadorMul> <expressioUnaria> <expressioMulPrima> | ε
<operadorMul> ::= % | / | *
<expressioUnaria> ::= - <expressioUnaria> | <factor>
<factor> ::= <constant> | <mutable>
<mutable> ::= ID <continuacioMutable>
<continuacioMutable> ::= ( <arguments> ) | -> ID <continuacioMutablePrima>
<continuacioMutablePrima> ::= ++ | -- | ε
<arguments> ::= <llistaArguments> | ε
<llistaArguments> ::= <expressio> <llistaArgumentsPrima>
<llistaArgumentsPrima> ::= , <expressio> <llistaArgumentsPrima> | ε
<constant> ::= 1 | STRING | true | false